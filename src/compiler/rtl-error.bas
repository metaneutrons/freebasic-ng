'' intrinsic runtime lib error functions (ERROR, ERR, ERL, RESUME, ...)
''
'' chng: oct/2004 written [v1ctor]

#include once "fb.bi"
#include once "fbint.bi"
#include once "parser.bi"
#include once "ast.bi"
#include once "rtl.bi"

	dim shared as FB_RTL_PROCDEF funcdata( 0 to ... ) = _
	{ _
		/' function fb_ErrorThrowAt cdecl _
			( _
				byval linenum as const long, _
				byval fname as const zstring ptr, _
				byval reslabel as const any ptr, _
				byval resnxtlabel as const any ptr _
			) as FB_ERRHANDLER '/ _
		( _
			@FB_RTL_ERRORTHROW, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_CDECL, _
			NULL, FB_RTL_OPT_NONE, _
			5, _
			{ _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_VOID ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_VOID ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeMultAddrOf( FB_DATATYPE_VOID, 2 ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorThrowEx cdecl _
			( _
				byval errnum as const long, _
				byval linenum as const long, _
				byval fname as const zstring ptr, _
				byval reslabel as const any ptr, _
				byval resnxtlabel as const any ptr _
			) as FB_ERRHANDLER '/ _
		( _
			@FB_RTL_ERRORTHROWEX, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_CDECL, _
			NULL, FB_RTL_OPT_NONE, _
			6, _
			{ _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_VOID ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_VOID ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeMultAddrOf( FB_DATATYPE_VOID, 2 ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorSetHandler( byval newhandler as FB_ERRHANDLER ) as FB_ERRHANDLER '/ _
		( _
			@FB_RTL_ERRORSETHANDLER, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			1, _
			{ _
				( typeAddrOf( FB_DATATYPE_VOID ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorHandlerPush( byval ctx as any ptr ptr, byval newhandler as FB_ERRHANDLER ) as any ptr '/ _
		( _
			@FB_RTL_ERRORHANDLERPUSH, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			2, _
			{ _
				( typeMultAddrOf( FB_DATATYPE_VOID, 2 ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( FB_DATATYPE_VOID ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' sub fb_ErrorHandlerExit( byval ctx as any ptr ptr ) '/ _
		( _
			@FB_RTL_ERRORHANDLEREXIT, NULL, _
			FB_DATATYPE_VOID, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			1, _
			{ _
				( typeMultAddrOf( FB_DATATYPE_VOID, 2 ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorGetNum( ) as long '/ _
		( _
			@FB_RTL_ERRORGETNUM, NULL, _
			FB_DATATYPE_LONG, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			0 _
		), _
		/' function fb_ErrorSetNum( byval errnum as const long ) as long '/ _
		( _
			@FB_RTL_ERRORSETNUM, NULL, _
			FB_DATATYPE_LONG, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			1, _
			{ _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorResume cdecl( ) as any ptr '/ _
		( _
			@FB_RTL_ERRORRESUME, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_CDECL, _
			NULL, FB_RTL_OPT_NONE, _
			0 _
		), _
		/' function fb_ErrorResumeNext cdecl( ) as any ptr '/ _
		( _
			@FB_RTL_ERRORRESUMENEXT, NULL, _
			typeAddrOf( FB_DATATYPE_VOID ), FB_FUNCMODE_CDECL, _
			NULL, FB_RTL_OPT_NONE, _
			0 _
		), _
		/' function erl( ) as long '/ _
		( _
			@"erl", @"fb_ErrorGetLineNum", _
			FB_DATATYPE_LONG, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			0 _
		), _
		/' function erfn( ) as zstring ptr '/ _
		( _
			@"erfn", @"fb_ErrorGetFuncName", _
			typeAddrOf( FB_DATATYPE_CHAR ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NOQB, _
			0 _
		), _
		/' function ermn( ) as zstring ptr '/ _
		( _
			@"ermn", @"fb_ErrorGetModName", _
			typeAddrOf( FB_DATATYPE_CHAR ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NOQB, _
			0 _
		), _
		/' function fb_ErrorSetModName( byval mod_name as const zstring ptr ) as zstring ptr '/ _
		( _
			@FB_RTL_ERRORSETMODNAME, NULL, _
			typeAddrOf( FB_DATATYPE_CHAR ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			1, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' function fb_ErrorSetFuncName( byval fun_name as const zstring ptr ) as zstring ptr '/ _
		( _
			@FB_RTL_ERRORSETFUNCNAME, NULL, _
			typeAddrOf( FB_DATATYPE_CHAR ), FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_NONE, _
			1, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' sub fb_Assert overload _
			( _
				byval filename as const zstring ptr, _
				byval linenum as const long, _
				byval funcname as const zstring ptr, _
				byval expression as const zstring ptr _
			) '/ _
		( _
			@"fb_Assert", NULL, _
			FB_DATATYPE_VOID, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_OVER, _
			4, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeSetIsConst( FB_DATATYPE_INTEGER ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' sub fb_Assert overload alias "fb_AssertW" _
			( _
				byval filename as const zstring ptr, _
				byval linenum as const long, _
				byval funcname as const zstring ptr, _
				byval expression as const wstring ptr _
			) '/ _
		( _
			@"fb_Assert", @"fb_AssertW", _
			FB_DATATYPE_VOID, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_OVER, _
			4, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_WCHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' sub fb_AssertWarn overload _
			( _
				byval filename as const zstring ptr, _
				byval linenum as const long, _
				byval funcname as const zstring ptr, _
				byval expression as const zstring ptr _
			) '/ _
		( _
			@"fb_AssertWarn", NULL, _
			FB_DATATYPE_VOID, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_OVER, _
			4, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' sub fb_AssertWarn overload alias "fb_AssertWarnW" _
			( _
				byval fname as const zstring ptr, _
				byval linenum as const long, _
				byval funcname as const zstring ptr, _
				byval expression as const wstring ptr _
			) '/ _
		( _
			@"fb_AssertWarn", @"fb_AssertWarnW", _
			FB_DATATYPE_VOID, FB_FUNCMODE_FBCALL, _
			NULL, FB_RTL_OPT_OVER, _
			4, _
			{ _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeSetIsConst( FB_DATATYPE_LONG ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_CHAR ) ), FB_PARAMMODE_BYVAL, FALSE ), _
				( typeAddrOf( typeSetIsConst( FB_DATATYPE_WCHAR ) ), FB_PARAMMODE_BYVAL, FALSE ) _
			} _
		), _
		/' EOL '/ _
		( _
			NULL _
		) _
	 }

'':::::
sub rtlErrorModInit( )

	rtlAddIntrinsicProcs( @funcdata(0) )

end sub

'':::::
sub rtlErrorModEnd( )

	'' procs will be deleted when symbEnd is called

end sub

'' Whether the error path emits the resume labels and hands their addresses to
'' the runtime.  -ex always does; the C backend does so even without RESUME
'' support, because it emits the jump to the error handler as `goto *ptr` and
'' clang rejects an indirect goto in a function that takes no label address
'' ("indirect goto in function with no address-of-label expressions").  Without
'' these labels every procedure containing an error check is such a function,
'' so `fbc -e` cannot be compiled at all wherever clang translates the emitted
'' C -- which the compiler cannot tell from the target: Darwin's cc is clang,
'' the win32 aarch64 toolchain ships clang as its gcc, and any host can have a
'' clang behind the name.  The C backend therefore always emits them, which is
'' the shape -ex produces anyway.  The assembly backends emit the jump
'' themselves and are left alone.
private function hEmitResumeLabels( ) as integer
	if( env.clopt.resumeerr ) then
		function = TRUE
		exit function
	end if

	select case as const( env.clopt.backend )
	case FB_BACKEND_GCC, FB_BACKEND_CLANG
		function = TRUE
	case else
		function = FALSE
	end select
end function

'' A context lives in an implicit local pointer.  The runtime allocates the
'' jmp_buf behind that pointer, while its address identifies this procedure's
'' activation when deciding whether RESUME remains valid after a longjmp.
private sub hErrorHandlerAddInit( byval proc as FBSYMBOL ptr )
	with proc->proc.ext->err
		if( .ctx <> NULL ) then
			exit sub
		end if

		.ctx = symbAddImplicitVar( typeAddrOf( FB_DATATYPE_VOID ), NULL, FB_SYMBOPT_UNSCOPE )
		astAddUnscoped( astNewDECL( .ctx, TRUE ) )
		symbSetIsDeclared( .ctx )
	end with
end sub

private function hErrorHandlerContext( ) as ASTNODE ptr
	dim as FBSYMBOL ptr ctx = parser.currproc->proc.ext->err.ctx

	if( ctx <> NULL ) then
		function = astNewADDROF( astNewVAR( ctx ) )
	else
		function = astNewCONSTi( NULL, FB_DATATYPE_UINT )
	end if
end function

private function rtlErrorHandlerPush _
	( _
		byval ctx as ASTNODE ptr, _
		byval newhandler as ASTNODE ptr _
	) as ASTNODE ptr

	dim as ASTNODE ptr proc = astNewCALL( PROCLOOKUP( ERRORHANDLERPUSH ) )

	if( astNewARG( proc, ctx ) = NULL ) then
		return NULL
	end if
	if( astNewARG( proc, newhandler ) = NULL ) then
		return NULL
	end if

	function = proc
end function

private function hErrorThrow _
	( _
		byval reslabel as FBSYMBOL ptr, _
		byval nxtlabel as FBSYMBOL ptr _
	) as ASTNODE ptr

	dim as ASTNODE ptr proc = any, param = any

	'' fb_ErrorThrow( linenum, module, reslabel, resnxtlabel, sourcectx )
	proc = astNewCALL( PROCLOOKUP( ERRORTHROW ) )

	'' linenum
	astNewARG( proc, astNewCONSTi( lexLineNum() ) )

	'' module
	astNewARG( proc, astNewCONSTstr( env.inf.name ) )

	'' reslabel
	if( reslabel ) then
		param = astNewADDROF( astNewVAR( reslabel ) )
	else
		param = astNewCONSTi( NULL, FB_DATATYPE_UINT )
	end if
	astNewARG( proc, param )

	'' resnxtlabel
	if( hEmitResumeLabels( ) ) then
		param = astNewADDROF( astNewVAR( nxtlabel ) )
	else
		param = astNewCONSTi( NULL, FB_DATATYPE_UINT )
	end if
	astNewARG( proc, param )

	'' The runtime invalidates RESUME when this error unwinds into a handler
	'' installed by a different procedure.
	astNewARG( proc, hErrorHandlerContext( ) )

	'' All the astNewARG()'s should succeed, they're hard-coded, not
	'' supplied by the input code
	assert( proc->call.args = 5 )

	function = proc
end function

'' Note: The rtl error checking code needs to be LINKed with the statement it
'' checks, in order to ensure the resulting single astAdd() will clean up any
'' temp vars from the statement after the whole rtl error checking code,
'' not in the middle of it, where it'd be either too early (during the astAdd()
'' for the reslabel, that'd be before the statement even was executed), or
'' just on one code path of the IF (if it's the astAdd() for the IF check).
'' (astBuildBranch() could be used to fix the 2nd issue, but that still leaves
'' the 1st)
function rtlErrorCheck( byval expr as ASTNODE ptr ) as ASTNODE ptr
	dim as FBSYMBOL ptr nxtlabel = any, reslabel = any
	dim as ASTNODE ptr t = NULL

	if( hEmitResumeLabels( ) ) then
		reslabel = symbAddLabel( NULL )
		t = astNewLINK( t, astNewLABEL( reslabel ), AST_LINK_RETURN_NONE )
	else
		reslabel = NULL
	end if

	if( env.clopt.errorcheck ) then
		'' if expr = 0 then
		nxtlabel = symbAddLabel( NULL )
		t = astNewLINK( t, astNewBOP( AST_OP_EQ, expr, astNewCONSTi( 0 ), nxtlabel, AST_OPOPT_NONE ), AST_LINK_RETURN_NONE )

		'' fb_ErrorThrow()
		t = astNewLINK( t, astNewBRANCH( AST_OP_JUMPPTR, NULL, hErrorThrow( reslabel, nxtlabel ) ), AST_LINK_RETURN_NONE )

		'' end if
		t = astNewLINK( t, astNewLABEL( nxtlabel ), AST_LINK_RETURN_NONE )
	else
		t = astNewLINK( t, expr, AST_LINK_RETURN_NONE )
	end if

	function = t
end function

sub rtlErrorThrow _
	( _
		byval errexpr as ASTNODE ptr, _
		byval linenum as integer, _
		byval module as zstring ptr _
	)

	dim as ASTNODE ptr proc = any, param = any
	dim as FBSYMBOL ptr nxtlabel = any, reslabel = any

	''
	proc = astNewCALL( PROCLOOKUP( ERRORTHROWEX ) )

	''
	reslabel = symbAddLabel( NULL )
	astAdd( astNewLABEL( reslabel ) )

	nxtlabel = symbAddLabel( NULL )

	'' fb_ErrorThrowEx( errnum, linenum, module, reslabel, resnxtlabel, sourcectx );

	'' errnum
	if( astNewARG( proc, errexpr ) = NULL ) then
		exit sub
	end if

	'' linenum
	if( astNewARG( proc, astNewCONSTi( linenum ) ) = NULL ) then
		exit sub
	end if

	'' module
	if( astNewARG( proc, astNewCONSTstr( module ) ) = NULL ) then
		exit sub
	end if

	'' reslabel
	if( hEmitResumeLabels( ) ) then
		param = astNewADDROF( astNewVAR( reslabel ) )
	else
		param = astNewCONSTi( NULL, FB_DATATYPE_UINT )
	end if
	if( astNewARG( proc, param ) = NULL ) then
		exit sub
	end if

	'' resnxtlabel
	if( hEmitResumeLabels( ) ) then
		param = astNewADDROF( astNewVAR( nxtlabel ) )
	else
		param = astNewCONSTi( NULL, FB_DATATYPE_UINT )
	end if
	if( astNewARG( proc, param ) = NULL ) then
		exit sub
	end if

	'' sourcectx
	if( astNewARG( proc, hErrorHandlerContext( ) ) = NULL ) then
		exit sub
	end if

	'' dst
	astAdd( astNewBRANCH( AST_OP_JUMPPTR, NULL, proc ) )

	astAdd( astNewLABEL( nxtlabel ) )
end sub

'':::::
sub rtlErrorSetHandler _
	( _
		byval newhandler as ASTNODE ptr, _
		byval savecurrent as integer _
	)

	dim as FBSYMBOL ptr handler_label = any, continue_label = any
	dim as ASTNODE ptr ctx = any, jumpbuf = any

	'' savecurrent was needed by the old global-handler implementation.  The
	'' context is now per procedure activation, so every ON ERROR statement
	'' uses the same local context and the runtime restores its predecessor at
	'' procedure exit.
	hErrorHandlerAddInit( parser.currproc )
	ctx = hErrorHandlerContext( )
	jumpbuf = rtlErrorHandlerPush( ctx, astCloneTree( newhandler ) )

	'' setjmp() returns 0 during handler installation, so it skips the handler
	'' jump. A runtime error in a nested procedure returns here nonzero and
	'' reaches the target label with this procedure's frame restored.
	handler_label = symbAddLabel( NULL )
	continue_label = symbAddLabel( NULL )
	astAdd( astBuildBranch( _
		astNewBOP( AST_OP_EQ, rtlSetJmp( jumpbuf ), astNewCONSTi( 0 ) ), _
		handler_label, _
		FALSE ) )
	astAdd( astNewBRANCH( AST_OP_JMP, continue_label ) )
	astAdd( astNewLABEL( handler_label ) )
	astAdd( astNewBRANCH( AST_OP_JUMPPTR, NULL, newhandler ) )
	astAdd( astNewLABEL( continue_label ) )

end sub

sub rtlErrorHandlerExit( byval ctx as ASTNODE ptr )
	dim as ASTNODE ptr proc = astNewCALL( PROCLOOKUP( ERRORHANDLEREXIT ) )

	if( astNewARG( proc, ctx ) <> NULL ) then
		astAdd( proc )
	end if
end sub

'':::::
function rtlErrorGetNum _
	( _
		_
	) as ASTNODE ptr

	''
	function = astNewCALL( PROCLOOKUP( ERRORGETNUM ) )

end function

'':::::
sub rtlErrorSetNum _
	( _
		byval errexpr as ASTNODE ptr _
	)

	dim as ASTNODE ptr proc = any

	''
	proc = astNewCALL( PROCLOOKUP( ERRORSETNUM ) )

	'' byval errnum as integer
	if( astNewARG( proc, errexpr ) = NULL ) then
		exit sub
	end if

	''
	astAdd( proc )

end sub

sub rtlErrorResume( byval isnext as integer )
	dim as ASTNODE ptr proc = any
	dim as FBSYMBOL ptr f = any

	if( isnext = FALSE ) then
		f = PROCLOOKUP( ERRORRESUME )
	else
		f = PROCLOOKUP( ERRORRESUMENEXT )
	end if

	proc = astNewCALL( f )

	astAdd( astNewBRANCH( AST_OP_JUMPPTR, NULL, proc ) )
end sub

'':::::
function rtlErrorSetModName _
	( _
		byval sym as FBSYMBOL ptr, _
		byval modname as ASTNODE ptr _
	) as ASTNODE ptr

	dim as ASTNODE ptr proc = any, expr = any

	proc = astNewCALL( PROCLOOKUP( ERRORSETMODNAME ) )

	'' byval module as zstring ptr
	if( astNewARG( proc, modname ) = NULL ) then
		return NULL
	end if

	if( sym <> NULL ) then
		with sym->proc.ext->err
			.lastmod = symbAddTempVar( typeAddrOf( FB_DATATYPE_CHAR ) )
			expr = astNewVAR( .lastmod )
			function = astNewASSIGN( expr, proc )
		end with
	else
		function = proc
	end if

end function

'':::::
function rtlErrorSetFuncName _
	( _
		byval sym as FBSYMBOL ptr, _
		byval funcname as ASTNODE ptr _
	) as ASTNODE ptr

	dim as ASTNODE ptr proc = any, expr = any

	proc = astNewCALL( PROCLOOKUP( ERRORSETFUNCNAME ) )

	'' byval function as zstring ptr
	if( astNewARG( proc, funcname ) = NULL ) then
		return NULL
	end if

	if( sym <> NULL ) then
		with sym->proc.ext->err
			.lastfun = symbAddTempVar( typeAddrOf( FB_DATATYPE_CHAR ) )
			expr = astNewVAR( .lastfun )
			function = astNewASSIGN( expr, proc )
		end with
	else
		function = proc
	end if

end function
